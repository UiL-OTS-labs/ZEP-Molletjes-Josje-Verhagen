/******************************************************************************\
FILE:           test_page.zm
AUTHOR:         Chris van Run UiL-OTS <C.P.A.vanrun@uu.nl>
ADAPTED BY:     -

DESCRIPTION:

Provides a page object to show to the participant during test trials.
It handles presenting the stimulus and recording the participant's response.


HISTORY:
????????? Cvr   Created.

\******************************************************************************/

import std_sound_output_device;

Page test_page
{
    TestItem    item;           // trial control parameters

    init()
    {
        fill_pattern_color = color:lightgray;
    }

    on_event:key_press()
    {	
		if(input_key == KEY_Escape)
		{
			response.correct = -1;
			done(CONTINUE);
		}
		else if(input_key == ' ')
			response.process_hit(event_time);
		else
			handle_special_key(this, input_key, input_modifiers);
    }

    on_event:message()
    {
       if(message_sender == control)
			response.process_hit(event_time);
    }
    
	StackLayout layout
	{
		CanvasGadget background
		{
			on_event:resize()
			{
				offset_x = round(actual_width / 2);
				offset_y = round(actual_height / 2);
			}
			
			ImageShape image
			{
				void load(string fn)
				{
					image = fn;
					height = round(background.height);
					width  = round(image_width * (height/image_height));
					x = -round(actual_width / 2);
					y = -round(actual_height / 2);
					
					is_visible = true;
				}
			}
		}

        CanvasGadget canvas
        {
			dur fade_in_dur;
			int num_frames_fade_in; 
			
			on_event:resize()
			{
				offset_x = round(test_window1.width / 2);
				offset_y = round(test_window1.height / 2);
			}
			
			init()
			{
				fade_in_dur = IMAGE_FADE_IN_DURATION;
				num_frames_fade_in = int(fade_in_dur/display_device.refresh_interval); 
			}
            
            ImageShape normal_image1
			{
				pre_update()
				{
					if(relative_frame <= num_frames_fade_in )
						this.alpha = real(relative_frame)/real(num_frames_fade_in);
					else if(reverse_relative_frame <= num_frames_fade_in)
						this.alpha = real(reverse_relative_frame)/real(num_frames_fade_in);
					else
						this.alpha = 1.0;
				}
				
				init()
				{
					image = stimuli_dir() + "images/" + NORMAL_IMAGE_FILENAME;  //this is adjusted in the setup().
					height = NORMAL_IMAGE_HEIGHT;
					width  = round(image_width * (height/image_height));
					x = round(-width/2); 					//center on canvas
					y = round(-height/2);					
					is_visible = false;
				}
			}
			
			ImageShape normal_image2
			{
				pre_update()
				{
					if(relative_frame <= num_frames_fade_in )
						this.alpha = real(relative_frame)/real(num_frames_fade_in);
					else if(reverse_relative_frame <= num_frames_fade_in)
						this.alpha = real(reverse_relative_frame)/real(num_frames_fade_in);
				}
				
				init()
				{
					image = stimuli_dir() + "images/" + NORMAL_IMAGE_FILENAME;  //this is adjusted in the setup().
					height = NORMAL_IMAGE_HEIGHT;
					width  = round(image_width * (height/image_height));
					x = round(-width/2); 					//center on canvas
					y = round(-height/2);					
					is_visible = false;
				}
			}
			
			ImageShape normal_image3
			{
				pre_update()
				{
					if(relative_frame <= num_frames_fade_in )
						this.alpha = real(relative_frame)/real(num_frames_fade_in);
					else if(reverse_relative_frame <= num_frames_fade_in)
						this.alpha = real(reverse_relative_frame)/real(num_frames_fade_in);
				}
				
				init()
				{
					image = stimuli_dir() + "images/" + NORMAL_IMAGE_FILENAME;  //this is adjusted in the setup().
					height = NORMAL_IMAGE_HEIGHT;
					width  = round(image_width * (height/image_height));
					x = round(-width/2); 					//center on canvas
					y = round(-height/2);					
					is_visible = false;
				}
			}
			
			 ImageShape reward_image
			{
				init()
				{
					image = stimuli_dir() + "images/" + REWARD_IMAGE_FILENAME;  //this is adjusted in the setup().
					height = NORMAL_IMAGE_HEIGHT;
					width  = round(image_width * (height/image_height));
					x = round(-width/2); 					//center on canvas
					y = round(-height/2);					
					is_visible = false;
				}
			}
        }
        
        void reset()
			{
				full_abort();
				
				layout.canvas.normal_image1.is_visible = false;
				layout.canvas.normal_image2.is_visible = false;
				layout.canvas.normal_image3.is_visible = false;
				layout.canvas.reward_image.is_visible = false;
			}
    }
	
    SoundChain soundchain
        {
			SoundSequence seq
				{
					SoundFile clip1 {}
					SoundFile clip2 {}
					SoundFile clip3 {}
				}
			
			SoundPlayback playback {}
			
			PlayBackTiming play(time t) //returns an array with 
				{
					abort(); //abort any playing sounds in the specific soundchain;
					
					playback.device = sound_output_device;
					PlayBackTiming timing; //holds the timing info
					
					seq.clip1.file = stimuli_dir() + "sounds/" + item.sndfnA;
					seq.clip1.trailer_duration = INTER_AUDIO_DURATION;
					
					timing.start_of_clip2 = t + seq.clip1.duration;
					seq.clip2.file = stimuli_dir() + "sounds/" + item.sndfnB;
					timing.end_of_clip2 = timing.start_of_clip2  + seq.clip2.duration; // do not count trailer.
					seq.clip2.trailer_duration = INTER_AUDIO_DURATION;
					
					timing.start_of_clip3 = timing.start_of_clip2 + seq.clip2.duration; //now trailer is counted
					seq.clip3.file = stimuli_dir() + "sounds/" + item.sndfnC;
					seq.clip3.trailer_duration = INTRA_AUDIO_DURATION;
										
					start(t);
					timing.final_end_time = timing.start_of_clip3 + seq.clip3.duration;
					return timing;
				}
				
				void reset()
				{
					seq.clip1.trailer_duration = 0;
					seq.clip2.trailer_duration = 0;
					seq.clip3.trailer_duration = 0;
				}
		
		}

    Response response
    {
        // Response data.
        int     rt;             // response time [ms]
        int     correct;        // correct response: 0=no, 1=yes, -1=n.a.


        void clear()
        {
            rt = -9999;
            correct = (item.sndfnC != FOCUS_WORD_FN[FOCUS_WORD]);
        }


        void process_hit(time t)
        {
            HitType validity = hit(t);

            // Accept the first hit in the response interval and end the 
            // response interval.
            if (validity == HIT_VALID)
            {
                rt = int(reaction_time(0));
                correct = (item.sndfnC == FOCUS_WORD_FN[FOCUS_WORD]); // if a response was required, this is correct. Else this is incorrect (0);
                
				if(correct == 1)
				{
					layout.canvas.normal_image3.full_abort(); // stop showing the third picture
					layout.canvas.normal_image3.is_visible = false; // hide the third picture
					layout.canvas.reward_image.start(t, IMAGE_SHOW_DURATION); // start showing the reward picture
					
					timeout.abort();
					timeout.start(t + IMAGE_SHOW_DURATION + INTRA_AUDIO_DURATION);
				}
				
                ignore_remaining_hits();
            }
            else if (validity == HIT_TOO_EARLY || validity == HIT_TOO_LATE)
            {
                // False alarm. Deal with this if desired.
            }
        }
		
        on_event:message()
        {
            done(CONTINUE);
        }
    }


    // Aborts any ongoing activity on this page and signals the initiator
    // of the trial that we're done.
    void done(int msgid)
    {
        // Just in case; abort presentation if still active.
        layout.reset();
        timeout.abort();
        
		soundchain.abort();
		soundchain.reset();
		
        signal_target(msgid);   // tell caller we're done
        target = null;

        control.clear_status();
    }


    //==========================================================================


    // Performs preparatory work required before using this page.
    void setup()
    {      		
		if(IMAGE_FADE_IN_DURATION >= (INTER_AUDIO_DURATION/2))
				println("WARNING: IMAGE_FADE_IN_DURATION (" + IMAGE_FADE_IN_DURATION + ") should be kept at below half the INTER_AUDIO_DURATION (" + INTER_AUDIO_DURATION + ")!");
		layout.background.image.load(stimuli_dir() + "images/" + BACKGROUND_IMAGE_FILENAME);
    }


    // Performs cleaning up if necessary.
    void cleanup()
    {
        if (control.target == this)
        {
            control.disable_keys();
            control.button_box.disable_buttons();
            control.target = null;
        }
    }


    // Starts a trial.
    void action(Object caller, TestItem it, time tref)
    {
        // Save the passed trial control parameters.
        item = it;
		
		 // Remember who to signal when the trial is over.
        target = caller;
        control.target = this;
        control.button_box.disable_buttons();
        control.disable_keys();
        control.button_box.enable_all_buttons(0); // every button --> 0
        
        // Show this page (if not yet done) and make tref the trial start time.
        tref = test_window1.show_test_page(this, tref, true);
        // Reset response data.
        response.clear();

		//set-up the play of audio
		PlayBackTiming timing = soundchain.play(tref);
		
		//set first image+sound to start adjust timings to allow for fade in periods.
		layout.canvas.normal_image1.start(tref-IMAGE_FADE_IN_DURATION, IMAGE_SHOW_DURATION + (2*IMAGE_FADE_IN_DURATION));
		layout.canvas.normal_image2.start(timing.start_of_clip2-IMAGE_FADE_IN_DURATION, IMAGE_SHOW_DURATION + (2*IMAGE_FADE_IN_DURATION));
		layout.canvas.normal_image3.start(timing.start_of_clip3-IMAGE_FADE_IN_DURATION, IMAGE_SHOW_DURATION + (2*IMAGE_FADE_IN_DURATION));
		
        // Response interval starts at stimulus onset. Timeout at the tref.
		response.start(timing.end_of_clip2, 0ms); // you have to responde within a certain amount of time
	
		timeout.start(timing.final_end_time); //provide feedback to the experimenter and continue if this is needed!
			
		//~ // with the 200ms400hz48kh24bit file (test.wav) it should be:
		//~ println(timing.start_of_clip2 - tref); //550
		//~ println(timing.end_of_clip2 - tref);// 750
		//~ println(timing.start_of_clip3 - tref);//1100
		//~ println(timing.final_end_time - tref);//2050
		
        // Update status line on control window. Cleared in done() above.
        control.set_status(
            string(item.id) + "  " + 
            string(item.sndfnA) + "  " + 
            string(item.sndfnB) + "  " + 
            string(item.sndfnC)
            );
    }
    
    Timer timeout
	{
		on_event:expire()
		{
			done(CONTINUE);
		}
	}
}



// holds timings for playbacktiming
record PlayBackTiming
{
	time start_of_clip2;
	time end_of_clip2;
	time start_of_clip3;
	time final_end_time;
}
